plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.vanniktech.maven.publish'
}

android {
    namespace = "com.cyphercove.covetools"
    compileSdk 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"
    }

    buildFeatures {
        buildConfig = false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }

}

import com.vanniktech.maven.publish.AndroidSingleVariantLibrary

mavenPublishing {
    coordinates(group, 'covetools-android', version)

    pom {
        name = 'CoveTools'
        description = 'Various tools for use with LibGDX.'
        url = 'https://github.com/CypherCove/CoveTools'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        organization {
            name = 'Cypher Cove'
            url = 'www.cyphercove.com'
        }
        developers {
            developer {
                id = 'cypher-cove'
                name = 'Cypher Cove'
                email = 'contact@cyphercove.com'
            }
        }
        scm {
            connection = 'scm:git:git@github.com:cyphercove/covetools.git'
            developerConnection = 'scm:git:git@cyphercove/covetools.git'
            url = 'https://github.com/cyphercove/CoveTools/'
        }
    }

    configure(new AndroidSingleVariantLibrary("release", true, true))
    // mavenCentralUsername and mavenCentralPassword should be set in ~/.gradle/gradle.properties
}
